use uo;
use os;

include "include/magic";
include "include/wodinc";

program activate_magic_trap (character, trap)
	if (character.cmdlevel or character.npctemplate)
		if (!GetObjProperty (character, "attackable") or character.concealed)
			return;
		endif
	endif
	
	if (character.dead)
		return;
	endif

	if (!ReserveItem (trap) )
		return;
	endif

	var traptype := GetObjProperty (trap, "traptype");
	if (!traptype)
		DestroyItem (trap);
		return;
	endif

	case (traptype)
		"flamestrike":
			var parms := {};
			parms [1] := "#SPECIALNONE";
			parms [2] := character;
			run_script_to_completion (":damage_spells:fstrike", parms);
		"darkness":
			foreach mobile in ListMobilesNearLocation (trap.x, trap.y, trap.z, 10)
				if (!mobile.npctemplate)
					PlaySoundEffect (mobile, SFX_SPELL_NIGHT_SIGHT );
					PlayObjectCenteredEffect (mobile, FX_BLESS_EFFECT, 10, 10);
					PrintTextAbove (mobile, mobile.name + " is blinded!");
					mobile.setlightlevel (30, 200);
				endif
			endforeach
		"poisonfield_ns":
			CreateField (0, trap.x, trap.y, trap.z, "poison", 80, "ns");
		"poisonfield_ew":
			CreateField (0, trap.x, trap.y, trap.z, "poison", 80, "ew");
		"firefield_ns":
			CreateField (0, trap.x, trap.y, trap.z, "fire", 80, "ns");
		"firefield_ew":
			CreateField (0, trap.x, trap.y, trap.z, "fire", 80, "ew");
		"energyfield_ns":
			CreateField (0, trap.x, trap.y, trap.z, "energy", 80, "ns");
		"energyfield_ew":
			CreateField (0, trap.x, trap.y, trap.z, "energy", 80, "ew");
		"parafield_ns":
			CreateField (0, trap.x, trap.y, trap.z, "para", 80, "ns");
		"parafield_ew":
			CreateField (0, trap.x, trap.y, trap.z, "para", 80, "ew");
		"stonewall_ns":
			CreateField (0, trap.x, trap.y, trap.z, "stone", 80, "ns");
		"stonewall_ew":
			CreateField (0, trap.x, trap.y, trap.z, "stone", 80, "ew");
		"explosion":
			foreach mobile in ListMobilesNearLocationEx (trap.x, trap.y, trap.z, 5, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN)
				PlaySoundEffect (mobile, SFX_SPELL_EXPLOSION);
				PlayObjectCenteredEffect (mobile, FX_EXPLODE_3, 7, 0x10);
				var dmg := RandomInt (40) + 20;
                    DoDamageByType (0, mobile, dmg, DAMAGETYPE_PHYSICAL);
			endforeach


	endcase

	DestroyItem (trap);

endprogram