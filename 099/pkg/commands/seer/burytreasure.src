use uo;
use os;

include "include/wodinc";
include "../pkg/npcs/npcinfo";
include "../pkg/npcs/npc_util";

program burytreasure( who, text )

	sendsysmessage(who,"Target the chest to bury");
	var chest := Target(who, TGTOPT_NOCHECK_LOS);
	if (!chest) SendSysMessage(who,"Canceled"); return; endif

	var map := createitemincontainer(who.backpack,0x6003,1);

	var level := cint(text);
	if (!level)
		sendsysmessage(who,"This will be a level 0, unlocked chest");
		chest.locked := 1;
		setobjproperty(map,"level",0);
	else
		sendsysmessage(who,"This chest will be locked with a " +(level*10)+ " difficulty");
		sendsysmessage(who,"This map will have a " +(level*10)+ " difficulty to decode");
		chest.locked := 1;
		SetObjProperty(chest,"level",level);
		SetObjProperty(chest,"seer",who.name);
		setobjproperty(map,"level",level);
	endif

	var guardians := {};

	sendsysmessage(who,"Target the guardians.  Hit escape when finished.");
	var npc := Target(who);
	while (npc.npctemplate)
		guardians.append (TrimTemplate (npc.npctemplate));
		KillNPC (npc);
		npc := Target( who );
	endwhile

	SetObjProperty(chest,"guardians", guardians);
	SetObjProperty(map,"chestx", who.x);
	SetObjProperty(map,"chesty", who.y);
	SetObjProperty(map,"chestz", who.z);
	SetObjProperty(map,"chestserial", chest.serial);

	chest.movable := 1;
	MoveObjectToLocation( chest, 5310, 1160, 0, realm := _DEFAULT_REALM, flags := MOVEOBJECT_FORCELOCATION );
	chest.movable := 0;
    
endprogram
