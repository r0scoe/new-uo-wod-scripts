use uo;
use os;
use util;

include "include/magic";
var spell_circle := 6;

program cast_mark (parms)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms))
		return 0;
	endif

	if (caster.x > 5118)
		sendsysmessage (caster, "Blocked!");
		return 0;
	endif

	//seal off Buccaneer's Den from outside gates
	if (caster.x > 2560 and caster.x < 2960 and caster.y > 1940 and caster.y < 2340)
		SendSysMessage (caster, "Your gate spell has somehow been interrupted.");
		return 0;
	endif

	SendSysmessage (caster, "Select a rune to mark.");
	var cast_on := Target (caster, TGTOPT_CHECK_LOS );
	if (!cast_on)
		return 0;
	endif

	if (!Accessible(caster, cast_on))
		SendSysmessage (caster, "You can't reach that!");
	endif

	if (cast_on.objtype != UOBJ_RECALL_RUNE)
		SendSysmessage(caster, "You can only cast that on a recall rune!");
		return 0;
	endif

	if (!ReserveItem(cast_on))
		SendSysMessage (caster, "You can't cast on that.");
		return 0;
	endif

	PlayObjectCenteredEffect (caster, FX_MARK_EFFECT, 10, 10);
	PlaySoundEffect (caster, SFX_SPELL_MARK);

	SetObjProperty (cast_on, "x", caster.x);
	SetObjProperty (cast_on, "y", caster.y);
	SetObjProperty (cast_on, "z", caster.z);
	SetObjProperty (cast_on, "realm", caster.realm);

	SetName (cast_on, "a marked recall rune");
	return 1;
endprogram
