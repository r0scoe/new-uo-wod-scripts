/* $Id: toolTips.src 104 2006-05-25 22:14:15Z montuz $
 *	Description: AOS Tooltips. Starter script.
 *	This packet takes awhile to get used to.
 *	HEAVILY modified by Agata
 */

use uo;
use cfgfile;
use polsys;

include "include/skills";
include "include/math";
include "include/itemtypes";

const OFFSET_OBJECT_SERIAL   := 0x05;
const OFFSET_CLILOC_ID       := 0x0F;
const OFFSET_LENGTH_ID       := 0x13;
const OFFSET_UNICODE_TEXT    := 0x14;

program itemdata(xObject)
	// Start of Loop.
	
	// This is where you'll be putting all your properties. <br> to break a new line.
	// You wont need to add anymore cliloc id's because you can just add all the props
	// into one string.
	var Html := "<BASEFONT COLOR=#FFFFFF><C>";
	var xDesc := "";
	// Examples :)
	var owner := GetObjProperty(xObject, "ownername");
	if (owner)
		xDesc := xDesc + Html + "Owned by " + owner + "<br>";
	endif
	if (GetObjProperty(xObject, "unidentified"))
		xDesc := xDesc + Html + "Unidentified<br>";
	else
		if (xObject.isa(POLCLASS_ARMOR))
			var armordesc := GetItemDescriptor(xObject.objtype);
			xDesc := xDesc + Html + "Armor Rating: " + xObject.ar + "<br>";
			if (armordesc.strrequired)
				xDesc := xDesc + Html + "Strength Required: " + CINT(armordesc.strrequired/10) + "<br>";
			endif
//			if (armordesc.DexPenalty)
//				xDesc := xDesc + Html + "-" + CINT(armordesc.DexPenalty/10) + " Dexterity" + "<br>";
//			endif
			if (xobject.resist_poison)
				xDesc := xDesc + Html + "Poison Protection: " + xObject.resist_poison + " charges<br>";
			endif
			var charges := GetObjProperty(xObject, "charges");
			if (charges)
				var spell := "";
				if (xobject.resist_fire)
					xDesc := xDesc + Html + "Fire Resistance: " + xObject.resist_fire + " charges<br>";
					spell := "f";
				endif
				if (xobject.resist_cold)
					xDesc := xDesc + Html + "Frost Resistance: " + xObject.resist_cold + " charges<br>";
					spell := "c";
				endif
				if (xobject.resist_energy)
					xDesc := xDesc + Html + "Electrical Resistance: " + xObject.resist_energy + " charges<br>";
					spell := "e";
				endif
				if (spell == "")
					spell := GetObjProperty(xObject, "itemspellname");
					if (!spell)
						spell := "Spell";
					endif
					xDesc := xDesc + Html + spell + " charges: " + charges + "<br>";
				endif
			endif
			var skillid := GetObjProperty(xObject, "skilladv");
			if (GetObjProperty (xObject, "skill"+skillid))
				var skillname := GetSkillName(skillid);
				xDesc := xDesc + Html + "+" + GetObjProperty (xObject, "skill"+skillid) + " " + skillname + "<br>";
			endif
			xDesc := xDesc + Html + "Durability: " + xObject.hp + " / " + xObject.maxhp + "<br>";
		elseif (xObject.isa(POLCLASS_WEAPON))
			if (GetObjProperty(xObject, "silver"))
				xDesc := xDesc + Html + "Silver Weapon<br>";
			endif
			var weapondesc := GetItemDescriptor(xObject.objtype);
			var damagefactor := (xObject.hp * xObject.quality)/xObject.maxhp;
			xDesc := xDesc + Html + "Damage: " + CINT((weapondesc.MinDamage + xObject.dmg_mod)*damagefactor) + " - " + CINT((weapondesc.MaxDamage + xObject.dmg_mod)*damagefactor) + "<br>";
			xDesc := xDesc + Html + "Skill: " + xObject.attribute + "<br>";
			var skillid := GetObjProperty(xObject, "skilladv");
			if (skillid)
				var skillname := GetSkillName(skillid);
				xDesc := xDesc + Html + "+" + GetObjProperty (xObject, "skill"+skillid) + " " + skillname + "<br>";
			endif
			var charges := GetObjProperty(xObject, "charges");
			if (charges)
				var spell := GetObjProperty(xObject, "itemspellname");
				if (!spell)
					spell := "Spell";
				endif
				xDesc := xDesc + Html + spell + " charges: " + charges + "<br>";
			endif
			xDesc := xDesc + Html + "Durability: " + xObject.hp + " / " + xObject.maxhp + "<br>";
		elseif (IsMusicalInstrument(xObject))
			var skillbonus := GetObjProperty(xObject, "skillbonus");
			if (skillbonus)
				xDesc := xDesc + Html + "+" + skillbonus + " Barding Skill<br>";
			endif
		elseif (IsJewelry(xObject))
			var stat_bonus := GetObjProperty (xObject, "str");
			if (stat_bonus)
				xDesc := xDesc + Html + "+" + stat_bonus + " Strength<br>";
			endif
			stat_bonus := GetObjProperty (xObject, "dex");
			if (stat_bonus)
				xDesc := xDesc + Html + "+" + stat_bonus + " Dexterity<br>";
			endif
			stat_bonus := GetObjProperty (xObject, "int");
			if (stat_bonus)
				xDesc := xDesc + Html + "+" + stat_bonus + " Intelligence<br>";
			endif
		elseif (xObject.isa(POLCLASS_CONTAINER))
			xDesc := xDesc + Html + "Contains: " + CINT(xObject.item_count-1) + " items<br>";
		elseif (xObject.isa(POLCLASS_SPELLBOOK))
			xDesc := xDesc + Html + xObject.spells().size() + " spells<br>";
		elseif (xObject.objtype == 0xA100)
			var spellcount := NecroSpellCount(xObject);
			xDesc := xDesc + Html + spellcount + " spells<br>";
		elseif (xObject.objtype == 0x1706b)
			var spellcount := DruidSpellCount(xObject);
			xDesc := xDesc + Html + spellcount + " spells<br>";		
		elseif (IsSign(xObject))
			owner := GetObjProperty (xObject, "lastownername");
			if (owner)
				xDesc := xDesc + Html + "Owner: " + owner + "<br>";
			endif
		endif
	endif
	if (!xObject.isa(POLCLASS_CORPSE) and !IsSign(xObject))
		xDesc := xDesc + Html + "Weight: " + xObject.weight + " stones<br>";
	endif
	if ( xObject.newbie )
		xDesc := xDesc + Html + "Blessed<br>";
	endif
	return xDesc;

endprogram

function NecroSpellCount(book)
	var GreaterFlag := CINT (GetObjProperty (book, "Greater"));
	var LesserFlag := CINT (GetObjProperty (book, "Lesser"));
	var spellcount := 0;
	for i := 1 to 8
		if (FlagCheck (LesserFlag, i))
			spellcount := spellcount + 1;
		endif
		if (FlagCheck (GreaterFlag, i))
			spellcount := spellcount + 1;
		endif
	endfor
	return spellcount;
endfunction

function DruidSpellCount(bag)
	var runes := GetObjProperty (bag, "runes");
	if (runes)
		return runes.size();
	endif
	return 0;
endfunction

function IsSign (item)
	case (item.objtype)
		0xb95:
		0xb96:
		0xbd0:
		0xbd2:
		0xc43:
		0xc44:
		0x1f28:
		0x1f29:
		0x17060:
		0x1708a:
			return 1;
	endcase
	if (item.objtype >= 0xba3 and item.objtype <= 0xc0e)
		return 1;
	elseif (item.objtype >= 0x1297 and item.objtype <= 0x129e)
		return 1;
	endif
	return 0;
endfunction

