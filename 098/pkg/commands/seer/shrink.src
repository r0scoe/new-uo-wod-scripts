use os;
use uo;
use cfgfile;

include "include/client";
include "include/objtype";
include "include/utility";
include "include/attributes";

program textcmd_shrink (character, text)
	SendSysMessage (character, "Shrink what?");
	var mobile := Target (character);
	if (!mobile)
		SendSysMessage (character, "Canceled.");
		return;
	endif
	if (!mobile.npctemplate)
		SendSysMessage (character, "This can only be used on NPCs.");
		return;
	endif

	var dolly := CreateItemInContainer (character.backpack, UOBJ_BACKPACK, 1);
	if (!dolly)
		SendSysMessage (character, "Unable to create dolly!");
		return;
	endif

	SetObjProperty (dolly, "npctemplate", mobile.npctemplate);
	SetObjProperty (dolly, "script", mobile.script);
	SetObjProperty (dolly, "graphic", mobile.graphic);
	dolly.color := mobile.color;
	dolly.name := mobile.name;
	dolly.newbie := 1;

	var trackingcfg := ReadConfigFile (":tracking:tracking");
	var elem := FindConfigElem (trackingcfg, mobile.graphic);
	if (elem)
		dolly.graphic := elem.tile;
	else
		dolly.graphic := 0x3a;
	endif

	foreach property in GetObjPropertyNames (mobile)
		var value := GetObjProperty (mobile, property);
		SetObjProperty (dolly, property, value);
	endforeach

	var equippack := CreateItemInContainer (dolly, UOBJ_BACKPACK, 1);
	SetObjProperty (dolly, "equippack", equippack.serial);
	foreach item in ListEquippedItems (mobile)
		if (item != mobile.backpack)
			copyitem (item, equippack);
		endif
	endforeach

	var lootbag := CreateItemInContainer (dolly, UOBJ_BACKPACK, 1);
	SetObjProperty (dolly, "lootbag", lootbag.serial);
	foreach item in ListRootItemsInContainer (mobile.backpack)
		copyitem (item, lootbag);
	endforeach

	SetObjProperty (dolly, "str", CINT (GetAttributeBaseValue (mobile, "Strength")/10));
	SetObjProperty (dolly, "int", CINT (GetAttributeBaseValue (mobile, "Intelligence")/10));
	SetObjProperty (dolly, "dex", CINT (GetAttributeBaseValue (mobile, "Dexterity")/10));

	var skills := {};
	for i := 0 to 48
		skills[i] := GetAttribute (mobile, GetAttributeIDBySkillID (i));
	endfor

	SetObjProperty (dolly, "skills", skills);
	SetObjProperty (dolly, "gender", mobile.gender);
	
	if (mobile.title_prefix)
		SetObjProperty (dolly, "title_prefix", mobile.title_prefix);
	endif
	if (mobile.title_suffix)
		SetObjProperty (dolly, "title_suffix", mobile.title_suffix);
	endif
	if (mobile.title_race)
		SetObjProperty (dolly, "title_race", mobile.title_race);
	endif
	if (mobile.title_guild)
		SetObjProperty (dolly, "title_guild", mobile.title_guild);
	endif

	dolly.usescript := ":dundee:shrunknpcs";
	SendSysMessage(character,"Done!");
endprogram

function CopyItem (item, container)
	var newitem := CreateItemInContainer (container, item.objtype, item.amount);
	if (!newitem)
		return 0;
	endif
	newitem.color := item.color;
	newitem.graphic := item.graphic;
	newitem.newbie := item.newbie;
	newitem.movable := item.movable;

	if (item.name)
		newitem.name := item.name;
	endif
	if (item.quality)
		newitem.quality := item.quality;
	endif
	if (item.maxhp_mod)
		newitem.maxhp_mod := item.maxhp_mod;
	endif
	if (item.hp)
		newitem.hp := item.hp;
	endif
	if (item.dmg_mod)
		newitem.dmg_mod := item.dmg_mod;
	endif
	if (item.ar_mod)
		newitem.ar_mod := item.ar_mod;
	endif
	if (item.usescript)
		newitem.usescript := item.usescript;
	endif
	if (item.hitscript)
		newitem.hitscript := item.hitscript;
	endif
	if (item.locked)
		newitem.locked := item.locked;
	endif
	foreach property in GetObjPropertyNames (item)
		var value := GetObjProperty (item, property);
		SetObjProperty (newitem, property, value);
	endforeach
	
	Syslog ("Checking " + newitem.desc + " to see if its a container");
	//recurse to copy subitems
	if (newitem.isa (POLCLASS_CONTAINER) or newitem.objtype == UOBJ_SPELLBOOK)
		Syslog (item.desc + " is a container!");
		foreach subitem in ListRootItemsInContainer (item)
			CopyItem (subitem, newitem);
		endforeach
	endif

	return newitem;
endfunction
