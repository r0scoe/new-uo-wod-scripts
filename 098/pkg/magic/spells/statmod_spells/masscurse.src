use uo;
use os;
use util;

include "include/magic";
var spell_circle := 6;

program cast_curse (parms)
	var caster := GetSpellCaster (parms);
	if (!caster)
		return 0;
	endif
	if (!CheckSuccessfulSpellCast (parms))
		return 0;
	endif
	var cast_loc := GetSpellCastOn (caster, parms);
	if (!cast_loc)
		return 0;
	endif

	var duration := CalcModDuration ();
	var mod_amount := CINT( (CalcModAmount() * 2) / 3);

	PlaySoundEffect(caster, SFX_SPELL_MASS_CURSE);
	foreach mobile in ListMobilesNearLocation (cast_loc.x, cast_loc.y, cast_loc.z, 4);
		if (CheckLineOfSight (caster, mobile) )
			mobile := CheckMagicReflection (caster, mobile);
			PlaySoundEffect (mobile, SFX_SPELL_MASS_CURSE);
			PlayObjectCenteredEffect (mobile, FX_CURSE_EFFECT, 10, 10);
			var resisted_mod_amount := Resisted (caster, mobile, mod_amount);
			DoMagicStatMod (mobile, STATMOD_ALL, 0-resisted_mod_amount, duration, "Curse Spell");
		endif
	endforeach
	return 1;
endprogram
