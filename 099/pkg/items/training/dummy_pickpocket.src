// dummy_pickpocket.src
//
// NOTE: No animation of the player
//
// HISTORY	Author: the Writer 05/01/99

use uo;
use os;
use attributes;

include "include/client";

Const UOBJECT_DUMMYSWING_EW := 0x1ec1;
Const UOBJECT_DUMMYFREEZE_EW := 0x1ec0;
Const UOBJECT_DUMMYSWING_NS := 0x1ec4;
Const UOBJECT_DUMMYFREEZE_NS := 0x1ec3;

program pickpocket_dummy( who , dummy )

	// don't work correctly, use for the future
	if( !Accessible( who, dummy ) )
	        SendSysmessage( who, "You can't reach that!" );
        	return;
	endif

	if( !CheckLoSAt( who, dummy.x, dummy.y, dummy.z ))
	        SendSysMessage( who, "You don't have line of sight to that." );
        	return;
	endif

	if( CheckSkill( who, SKILLID_STEALING, 10, 0 ))

		PlaySoundEffect( who , SFX_58 );		
		SendSysmessage( who, "You unnoticedly grip into the bag." );
		sleepms(3600);

	else
		set_critical(1);

		PlaySoundEffect( who , SFX_41 );
		SendSysmessage( who, "You foolishly grip into the bag." );

		if ( dummy.objtype == UOBJECT_DUMMYFREEZE_NS )
			dummy.graphic := UOBJECT_DUMMYSWING_NS; // dummy swing
			sleepms(3600); // show animation for (x) ms
			dummy.graphic := UOBJECT_DUMMYFREEZE_NS; // dummy freeze
			
		elseif ( dummy.objtype == UOBJECT_DUMMYFREEZE_EW )
			dummy.graphic := UOBJECT_DUMMYSWING_EW; // dummy swing
			sleepms(3600); // show animation for (x) ms
			dummy.graphic := UOBJECT_DUMMYFREEZE_EW; // dummy freeze
		endif

		set_critical(0);

	endif

endprogram